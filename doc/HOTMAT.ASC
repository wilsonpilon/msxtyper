10 ' PROGRAMA PPI
20 OPEN "grp:" FOR OUTPUT AS#1
30 DEFINT A-Z:COLOR 15,4,4:SCREEN 2,1,0:BEEP
40 ' DEFINE UM QUADRADO
50 SPRITE$(0)=CHR$(255)+CHR$(129)+CHR$(129)+CHR$(129)+CHR$(129)+CHR$(129)+CHR$(129)+CHR$(255)
60 ' DESENHA PPI E DECODER
70 LINE(16,0)-(55,191),15,B:BEEP:BEEP:LINE(74,39)-(97,189),15,B:BEEP:BEEP
80 ' DESENHA O TECLADO
90 FOR F=39 TO 189 STEP 15:LINE (120,F)-(240,F):BEEP:NEXT F:FOR F=120 TO 240 STEP15:LINE(F,39)-(F,189):BEEP:NEXT F
100 ' LIGACAO PPI-DECODER                    LIGACAO DECODER-TECLADO
110 FOR F=98TO122STEP8:LINE(55,F)-(74,F),15:BEEP:NEXTF:FOR F=47TO189STEP15:LINE(97,F)-(120,F),15:BEEP:NEXTF
120 ' LIGACAO TECLADO-PPI
130 E=0:FORF=35TO7STEP-4:LINE(55,F)-((128+E),F),15:E=E+15:BEEP:NEXTF:E=7:FORF=233TO128STEP-15:LINE(F,E)-(F,39),15:BEEP:E=E+4:NEXTF
140 ' COLOCA LETRAS NO TECLADO
150 FOR E=43 TO 132 STEP 15:FOR F=125 TO 240 STEP 15:PRESET(F,E):READ A$:PRINT#1,A$:BEEP:NEXT F,E
160 'COLOCA FUNCOES NA MATRIZ
170 FOR E=133 TO 164 STEP 15:FOR F=122 TO 237 STEP 15:PRESET(F,E):READ A$,B$:PRINT#1,A$:PRESET((F+6),E):PRINT#1,B$:BEEP:NEXT F,E
180 'COLOCA ACENTOS
190 PSET(140,65):PSET(143,65):PRESET(139,57):PRINT#1,CHR$(39):PRESET(155,64):PRINT#1,"'":PRESET(155,57):PRINT#1,CHR$(96):PRESET(230,72):PRINT#1,CHR$(94):PRESET(230,78):PRINT#1,CHR$(187)
200 ' COLOCA CURSOR
210 PRESET(127,163):PRINT#1,CHR$(&HCF):PRESET(140,162):PRINT#1,CHR$(&HCD):PRESET(155,165):PRINT#1,CHR$(&HCE):PRESET(168,163):PRINT#1,CHR$(&HD0)
220 'PINTA QUADROS NAO USADOS
230 FOR F=130TO235STEP15:PAINT(F,180),15:NEXT F
240 ' COLOCA LEGENDA PPI DECOD
250 FOR F=2TO40STEP8:READ A$:PRESET(28,F):PRINT#1,A$:PRESET(28,(F+88)):PRINT#1,A$:BEEP:NEXTF
260 READA$:PRESET(36,18):PRINT#1,A$:READ A$:PRESET(36,111):PRINT#1,A$
270 FORF=4TO28STEP8:READA$:PRESET(43,F):PRINT#1,A$:READA$:PRESET(43,(F+93)):PRINT#1,A$:NEXT F:PRESET(27,58):PRINT#1,"PPI"
280 FORF=63TO164STEP8:READA$:PRESET(82,F):PRINT#1,A$:NEXTF:CLOSE#1
290 ' ATUALIZA A VARREDURA
300 IF C=10THENC=0ELSEC=C+1
310 ' FAZ VARREDURA E LE TECLA
320 A=INP(170):B=(240ANDA)ORC:OUT170,B:D=INP(169):IFD=255THEN300
330 IF D=254THENX=0
340 IF D=253THENX=1
350 IF D=251THENX=2
360 IF D=247THENX=3
370 IF D=239THENX=4
380 IF D=223THENX=5
390 IF D=191THENX=6
400 IF D=127THENX=7
410 ' POSICIONA SPRITE
420 PUTSPRITE5,((225-X*15),(C*15+38)),10,0:BEEP:BEEP:BEEP:GOTO300
430 DATA 7,6,5,4,3,2,1,0,~,,,\,=,-,9,8,B,A,<,/,.,",",[,,J,I,H,G,F,E,D,C,R,Q,P,O,N,M,L,K,Z,Y,X,W,V,U,T,S
440 DATA F,3,F,2,F,1,C,O,C,A,G,R,C,T,S,H,R,E,S,L,B,S,S,T,T,A,E,S,F,5,F,4,,,,,,,,,D,E,I,N,H,O,S,P
450 DATA P,O,R,T,O,B,C,0,0,!,!,!,!,7,3,D,E,C,O,D,I,F,I,C,A,D,O,R
