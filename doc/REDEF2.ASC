10 REM REDEFINIDOR - SCREEN 0
20 SCREEN 0:WIDTH 38
30 CLS:PRINT "Redefinidor de Caracteres/0":PRINT
40 PRINT STRING$(8,219):FOR I=1 TO 8:PRINT CHR$(219);:FOR J=1 TO 6:PRINT CHR$(1);"G";:NEXT J:PRINT CHR$(219):NEXT I:PRINT STRING$(8,219)
50 PRINT:PRINT:INPUT "Caracter ";A$:IF LEN(A$)=2 AND LEFT$(A$,1)=CHR$(1) THEN I=8*(ASC(RIGHT$(A$,1))-64)+2048:GOTO 70
60 I=8*ASC(A$)+2048
70 LOCATE 0,3:FOR J=I TO I+7:B$=BIN$(VPEEK(J)\4):B$=STRING$(6-LEN(B$),48)+B$:PRINT CHR$(219);
80 FOR K=1 TO 6:IF MID$(B$,K,1)="1" THEN PRINT CHR$(219); ELSE PRINT CHR$(1);"G";
90 NEXT K:PRINT:NEXT J:X=0
100 LOCATE 0,X+3:PRINT ">";CHR$(8);
110 J$=INKEY$:IF J$=CHR$(30) THEN PRINT CHR$(219):X=X-1:IF X<0 THEN X=7
120 IF J$=CHR$(31) THEN PRINT CHR$(219):X=X+1:IF X>7 THEN X=0
130 IF J$=" "THEN 150 ELSE IF J$="F" THEN 30
140 GOTO 100
150 LOCATE 0,20:PRINT"            ":LOCATE 0,20:INPUT L$:IF LEN(L$)<>6THEN 150 ELSE VPOKE I+X,4*VAL("&B"+L$):LOCATE 1,X+3:FOR L=1 TO 6:IF MID$(L$,L,1)="1" THEN PRINT CHR$(219);ELSE PRINTCHR$(1);"G";
160 NEXT L
170 GOTO 100
